%%s 0/0/0
%%d D 1.1 15-Aug-24 15:20:25 tkb 1 0
%%c Initial checkin.
%%T
%%I 1
$ !
$ ! FIND_EXP.COM Lists expenses for current month
$ !
$ ASSIGN ACCOUNTING$DATA:[ACCOUNTING.EX.FILES]EXPENSE_FILE EXPENSE_FILE 
$ !
$ IF F$SEARCH("DO_EXP.COM") .NES. "" THEN DELETE DO_EXP.COM;*
$ TURN_OFF = "DEFINE SYS$OUTPUT NL:"
$ TURN_ON = "DEASSIGN SYS$OUTPUT"
$ 	TIMEX = F$TIME()
$	MONTH = F$CVTIME(TIMEX, , "MONTH")
$	IF F$EXTRACT(0, 1, MONTH) .EQS. "0" THEN MONTH = MONTH  - "0"
$	YEAR = F$EXTRACT(2, 2, F$CVTIME(TIMEX, , "YEAR"))
$ CLEAR
$ INQ/NOPUN MTHTOUSE "Month? <''MONTH'> "
$ WRITE SYS$OUTPUT ""
$ IF MTHTOUSE .EQS. "" THEN MTHTOUSE = MONTH
$ IF F$LENGTH(MTHTOUSE) .EQ. 1 THEN MTHTOUSE = "0" + MTHTOUSE
$ !
$ 	OPEN/WRITE OUTF DO_EXP.COM
$ 	WRITE OUTF "$ RUN DMS:SORT"
$ 	WRITE OUTF "EXPENSE_FILE"
$ 	WRITE OUTF ""
$ 	WRITE OUTF "Y"
$ 	WRITE OUTF ".VENDOR"
$ 	WRITE OUTF "E0600/ONLY"
$ 	WRITE OUTF ".DATE/EXT='5-6,1-2'/FILL"
$ 	WRITE OUTF "''YEAR'''MTHTOUSE'/ONLY"
$ 	WRITE OUTF ""
$ 	WRITE OUTF "A AND B"
$ 	CLOSE OUTF
$ CLEAR
$ WRITE SYS$OUTPUT ""
$ WRITE SYS$OUTPUT "Finding expenses for month ''MTHTOUSE'..."
$ WRITE SYS$OUTPUT ""
$ TURN_OFF
$ 	@DO_EXP.COM
$ 	DELETE/NOCON DO_EXP.COM;*
$ !
$ TURN_ON
$ RUN DMS:ASQ
DISPLAY .DATE .DESCRIPTION WIDTH 57 .EMPAMOUNT TOTALED TO TT:
EXIT
$ WRITE SYS$OUTPUT ""
%%E 1
