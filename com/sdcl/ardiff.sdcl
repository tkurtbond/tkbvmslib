!> ARDIFF.SDCL -- Compare archive file to the version in a SWTOOLS VOS archive.
        SS$_BADPARAM = %x0014
        TRUE = 1 .eq. 1
        FALSE = 1 .ne. 1
        opt_list = "/HELP/NODELETE/STDIFF"
        opt_list_len = f$length (opt_list)
        nodelete = FALSE
        diffcmd = "diff"
        debugging = FALSE

        i = 1
        while (f$extract (0, 1, p'i') .eqs. "-") {
            opt = p'i'
            if ((opt .eqs. "-?") .or (opt .eqs. "-H")) opt = "-HELP"
            which = f$extract(1, 3, opt)
            if (debugging) write sys$output "which: ", which
            ok = (which .nes. "") .and. f$locate ("/" + which, opt_list) .ne. opt_list_len
            if (ok)
                gosub OPT$'which'
            else {
                write sys$output "ardiff: Invalid option: ''opt'"
                exit SS$_BADPARAM
            }
            i = i + 1
        }

        # Our current source, to be compared to the version in an archive.
        filespec = p'i'
        if (filespec .eqs. "") {
            write sys$output "ardiff: no filespec specified"
            exit SS$_BADPARAM
        }

        i = i + 1
        arfile = p'i'
        if (arfile .eqs. "") {
            write sys$error "ardiff: no archive file specified"
            exit SS$_BADPARAM
        }

        # Diff defaults device and directory to that of first argument,
        # so fill those in for the current source using f$parse().
        fullspec = f$parse (filespec)
                        
        filename = f$parse (filespec,,, "NAME")
        filetype = f$parse (filespec,,, "TYPE")
        tmpfile = "SYS$SCRATCH:" + filename + filetype + "-TMP"
        write sys$output "arfile: ", arfile, " tmpfile: ", tmpfile
        ar p 'arfile' 'filespec' >'tmpfile'
        'diffcmd' 'tmpfile' 'fullspec'
        if (.not. nodelete) delete/log 'tmpfile';0
        else write sys$error "sardiff: temporary file ", tmpfile, " left behind."
        exit

OPT$hel:
        copy sys$input sys$error
// ardiff.sdcl -- Compare archive file to the version in a SWTOOLS VOS archive.
// Usage:
// 
//         ardiff [-h|-?|-help] [-stdiff] SRCFILE ARFILE
// 
// where SRCFILE is the source file you want to compare and ARFILE
// contains the SWTOOLS VOS archive containing the version to compare
// to.  The version in the archive file will be extracted to a
// temporary file file and compared to the version in the working
// directory.  The temporary file will be deleted unless
// 
// Options:
//        -? -h -help	 Display help message and exit.
//	  -stdiff        Use the SWTOOLS diff instead of VMS diff.
        exit

OPT$nod:                                # No Delete
	nodelete = TRUE
        return



OPT$std:
        diffcmd = "stdiff"
        return        
