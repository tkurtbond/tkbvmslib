%%s 0/0/0
%%d D 1.1 06-Sep-24 09:57:08 tkb 1 0
%%c List files common and different between the contents of a archive and the
%%c current directory.
%%T
%%I 1
!> ARUNCOVERED.SDCL - Check for files in current directory not in a archive.
    TRUE = 1 .eq. 1
    FALSE = 1 .ne. 1
    wso := write sys$output
    wse := write sys$output
    dbg = "!"

    debugging = f$type (aruncovered_debugging) .nes. ""
    if (debugging) dbg := write sys$error

    opt_list = "/HELP/IGNORED/"
    opt_list_len = f$length(opt_list)

    default_ignored_list = "OBJ,OLB,HLB,EXE,DIR,TMP"
    ignored_list = default_ignored_list
    

    param_idx = 1
    while (f$extract (0, 1, p'param_idx') .eqs. "-") {
        opt = p'param_idx'
        opt_len = f$length (opt)

        if ((opt .eqs. "-?") .or. (opt .eqs. "-H")) opt = "-HELP"

        which = f$extract(1, 3, opt)
        ok = (f$locate("/" + which, opt_list) .ne. opt_list_len) .and. \
             (which .nes. "")
        if (ok) 
            gosub OPT$'which'
        else
            write sys$output "Invalid Option: ''opt'"

        
        param_idx = param_idx + 1
    }

    arfile = p'param_idx'
    if (arfile .eqs. "") {
        wse "aruncovered: You must specify an archive file!"
        goto error_end
    }

    pid = f$getjpi ("", "PID")

    artmpfile = "ARUNCOVERED_AR-" + pid + ".TMP"
    ar t1 'arfile' >'artmpfile'
    if ($status .ne. 1) {
        wse "aruncovered: Unable to list the archive, exiting."
        goto error_end
    }

    arsorttmpfile = "ARUNCOVERED_ARSORT-" + pid + ".TMP"
    stsort 'artmpfile' >'arsorttmpfile'
    if ($status .ne. 1) {
        wse "aruncovered: Unablle to sort the archive listing, exiting."
        goto_error_end
    }

    listing_spec = "lr"
    for (i = 0; f$element (i, ",", ignored_list) .nes. ","; i = i + 1) {
        file_type  = f$edit (f$element (i, ",", ignored_list), "LOWERCASE")
        listing_spec = listing_spec + " | find -x ." + file_type
    }
    listing_spec = listing_spec + " | find -x " + f$edit (arfile, "LOWERCASE") + " | sort "

    shtmpfile = "ARUNCOVERED_SH-" + pid + ".TMP"
    lrtmpfile = "ARUNCOVERED_LR-" + pid + ".TMP"

    listing_spec = listing_spec + " >" + lrtmpfile

    dbg "ignored_list: ", ignored_list
    dbg "listing_spec: ", listing_spec
    
    open/write shtmp 'shtmpfile'
    write shtmp listing_spec
    close shtmp

    sh 'shtmpfile'
    if ($status .ne. 1) {
        wse "aruncovered: Unable to produce and sort the directory listing, exiting."
        goto error_end
    }

    wso "Only in the archive"
    wso "               Only in the directory"
    wso "                         In both the archive and the directory"
    wso "=============================================================="
    comm 'arsorttmpfile' 'lrtmpfile'
        
    gosub delete_tmpfiles
    exit

delete_tmpfiles:
    if (debugging) {
        wse "aruncovered: Debugging, not deleting temmpoary files."
        return
    }
    if (f$search (artmpfile + ";0") .nes. "") delete/log 'artmpfile';0
    if (f$search (arsorttmpfile + ";0") .nes. "") delete/log 'arsorttmpfile';0
    if (f$search (shtmpfile + ";0") .nes. "") delete/log 'shtmpfile';0
    if (f$search (lrtmpfile + ";0") .nes. "") delete/log 'lrtmpfile';0
    return ! delete_tmpfiles
    
error_end:
    gosub delete_tmpfiles
    exit 2


OPT$hel:
    copy sys$input sys$output
//Check for files in current directory not in a archive.
//
//usage: aruncovered [-HELP|-?|-H] [-ignored=IGNOREDLIST] 
//
//Options:
//
//    -ignored=IGNOREDLIST
//        IGNOREDLIST is a list of file types to be ignored, seperated by
//        commas.
//
//    -help, -? -h
//        Display this message and exit
//
//Default IGNOREDLIST:
    write sys$output "    ", default_ignored_list
    exit


OPT$ign:
     p = f$locate("=", opt)
     l = f$length(opt)
     if (p .ne. l)
         ignored_list = ignored_list + "," + f$extract(p+1, l, opt)
     return ! -IGNORED
%%E 1
