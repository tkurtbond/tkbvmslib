### setags.icn -- Produce an etags style TAGS file for Scheme, on MSDOS

### Simple Usage: iconx setags file ...
### For more detail, search for `procedure usage'.

### I'm assuming the etags file has a format like this:
###     <FF><NL>
###     filename,size-of-text-of-tags-in-bytes<NL>
###     name<DEL>line-no,char-no-from-beginning-of-file-of-char-before-def<NL>
### for each file indexed.

link options

global version_string,
    do_debug,				# do debug messages
    warnings,			        # issue warnings
    verbose,				# be noisy
    outf_name,				# output file name, defaults to TAGS
    listf_name,				# filename of file containing filenames
    listf,				#   to process, and the file
    errf_name,				# error file name
    errf,				#   and error file
    filenames,				# generator of file names to process
    out_mode				# append to output or not

global whitespace, scheme_id_cset



procedure main(args)
    local inf_name,			# name of input file
	  inf,				# input file
	  outf				# output file

    initial {
	version_string := "setags v0.1"
	whitespace := '\014 \t'
	scheme_id_cset := &letters ++ &digits ++ '._$%-?/+=><!$#%^&*:\''
    }

    if *args = 0 then usage()		# need at least one argument
    parse_options(args)

    if outf_name == "-" then
	outf := &output
    else {
	outf := open(outf_name, out_mode) |
	    stop("can't open output file:", outf_name)
    }

    every inf_name := !filenames do {
	inf := open(inf_name, "r") |
	    (write(errf, "can't open ", inf_name, "\n") & next)
	if \verbose then write(errf, inf_name, ":")
	find_tags_in_scheme_file(inf, inf_name, outf)
	close(inf)
    }

    if outf_name ~== "-" then close(outf)

end # main



procedure parse_options(args)		# figure out command line options
    local opts
    
    opts := options(args, "ad+e:hi:o:vVw?")
    if errf_name := \opts["e"] then {
	errf := open(errf_name, "w") |
	    stop("can't open error file: ", errf_name)
    } else
	errf := &errout
    
    if \opts["?"] | \opts["h"] then usage()
    do_debug := (\opts["d"] | 0)
    warnings := (\opts["w"] | &null)
    verbose := (\opts["v"] | &null)
    if \opts["V"] then write(errf, version_string)
    outf_name := (\opts["o"] | "TAGS")
    
    if listf_name := \opts["i"] then {	# get list of file names from a file
	if listf_name == "-" then	# instead of the command line
	    listf := &input
	else {
	    listf := open(listf_name, "r") |
		stop("can't open file of names: ", listf_name)
	}
	filenames := listf
    } else				# Get list of file names from the
	filenames := args		# command line.

    if \opts["a"] then			# we want to append
	out_mode := "wa"
    else
	out_mode := "w"
end # parse_options



record ENTRY(fun_name, fun_def, line_no, char_no)



procedure find_tags_in_scheme_file(inf, inf_name, outf)
    local line,				# the current source line
	  lline,			# current source line in lower case
	  line_no,			# the current line number
	  fun_name,			# the name of the function, if any
	  char_no,			# the current character number
	  start_of_def,			# saved position of start of definition
	  p,				# saved position of start of fun name
	  tags_list,			# list of function indices
	  t,				# temp string
	  tags_text_length,		# length of tags info for file
	  tags_table,			# table of tags text
	  fun_def,			# text of definition of function
	  i				# a counter

    tags_text_length := line_no := char_no := 0
    tags_table := table("")
    every line := !inf do {
	## scheme is case insensitive, so map line to
	## lower case for easy matching
	lline := line #map(line, &ucase, &lcase)
	line_no +:= 1
	lline ? {
	    ## lines are like [<space>][<definition>]
	    tab(many(whitespace))
	    start_of_def := &pos
	    if ="(def" then {
		tab(many(scheme_id_cset))
		tab(many(whitespace))
		p := &pos
		if fun_name := ((="("|"") & tab(many(scheme_id_cset))) then {
		    move(1)		# to get following space or paren
		    fun_def := line[start_of_def:&pos]
		    tags_text_length +:=
			## this was char_no+p
			save_entry(tags_table, ENTRY(fun_name, fun_def,
						     line_no, char_no))
		} else {
		    if /warnings then 
			write(errf, inf_name, ": ", line_no,
			      ": missing name?")
		}
	    } 
	}
	char_no +:= *line + 1		# for the newline
    }

    ## write out the tag info for this file
    if \verbose then writes(errf, "writing tags...")
    tags_list := sort(tags_table, 2)
    write(outf, "\014\n", inf_name, ",", tags_text_length) # tags header
    every t := !tags_list do {
	if do_debug > 0 then
	    write (errf, t[2], ": ", t[1])
	write(outf, t[1])
    }
    if \verbose then write(errf, "done")
end # find_tags_in_scheme_file



procedure stringize_entry(entry)	# make etags style index
    return entry.fun_def || "\177" || entry.line_no || "," || entry.char_no
end # stringize_entry



procedure save_entry(tags_table, entry)
    local t

    t := stringize_entry(entry)
    ## use fun_name and line_no so sort out in line order if duplicates.
    tags_table[t] := entry.fun_name || " " || entry.line_no
    if do_debug > 0 then 
	write (errf, entry.fun_name, ",", entry.line_no, ",",
	       entry.char_no)
    return *t + 1
end # save_entry



procedure usage()
    stop("_
usage: betags [-vVa] [-o output-file] [-i file-of-names] file ...\n_
\t-h    this message\n_
\t-?    this message\n_
\t-v    verbose mode\n_
\t-V    print version\n_
\t-a    append to output file\n_
\t-e error-file    name of error file.\n_
\t-o output-file    name of output file, defaults to TAGS\n_
\t-i file-of-names    name of file to get names of input files from.")
end # usage
